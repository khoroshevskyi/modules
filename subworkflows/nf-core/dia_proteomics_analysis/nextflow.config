/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    DIA Proteomics Analysis Subworkflow Configuration
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Configuration file for the dia_proteomics_analysis subworkflow that replicates
    the quantms DIA-NN workflow behavior using nf-core modules.

    This configuration sets up the ext.args for each DIA-NN module to match
    the default quantms behavior and parameters.
----------------------------------------------------------------------------------------
*/

process {

    // QUANTMSUTILS_GENERATECFG - Configuration generation
    withName: 'QUANTMSUTILS_DIANNCFG' {
        ext.args = ''
    }

    // DIANN_INSILICOLIBRARYGENERATION - In-silico library generation
    withName: 'DIANN_INSILICOLIBRARYGENERATION' {
        ext.args = {[
            meta.diann_cfg ?: '',
            params.min_pr_mz ? "--min-pr-mz ${params.min_pr_mz}" : '',
            params.max_pr_mz ? "--max-pr-mz ${params.max_pr_mz}" : '',
            params.min_fr_mz ? "--min-fr-mz ${params.min_fr_mz}" : '',
            params.max_fr_mz ? "--max-fr-mz ${params.max_fr_mz}" : '',
            params.allowed_missed_cleavages != null ? "--missed-cleavages ${params.allowed_missed_cleavages}" : '',
            params.min_peptide_length ? "--min-pep-len ${params.min_peptide_length}" : '',
            params.max_peptide_length ? "--max-pep-len ${params.max_peptide_length}" : '',
            params.min_precursor_charge ? "--min-pr-charge ${params.min_precursor_charge}" : '',
            params.max_precursor_charge ? "--max-pr-charge ${params.max_precursor_charge}" : '',
            params.max_mods != null ? "--var-mods ${params.max_mods}" : '',
            params.diann_debug ? "--verbose ${params.diann_debug}" : '',
            "--fasta-search",
            "--predictor",
            "--gen-spec-lib",
            "--met-excision"
        ].findAll { it != '' }.join(' ').trim()}
        cpus = 10
    }

    // DIANN_PRELIMINARYANALYSIS - Preliminary analysis
    // Note: mass_acc and scan_window are NOT set during preliminary analysis in quantms
    // when using automatic modes - they're determined from the preliminary results
    withName: 'DIANN_PRELIMINARYANALYSIS' {
        ext.args = [
            params.diann_debug ? "--verbose ${params.diann_debug}" : '',
            params.quick_mass_acc ? "--quick-mass-acc" : '',
            params.performance_mode ? "--min-corr 2 --corr-diff 1 --time-corr-only" : ''
        ].findAll { it != '' }.join(' ').trim()
    }

    // DIANN_ASSEMBLEEMPIRICALLIBRARY - Empirical library assembly
    withName: 'DIANN_ASSEMBLEEMPIRICALLIBRARY' {
        ext.args = [
            params.diann_debug ? "--verbose ${params.diann_debug}" : '',
            "--rt-profiling",
            "--use-quant",
            "--gen-spec-lib",
            params.mass_acc_automatic ? "--individual-mass-acc" : '',
            params.scan_window_automatic ? "--individual-windows" : (params.scan_window ? "--window ${params.scan_window}" : '')
        ].findAll { it != '' }.join(' ').trim()
    }

    // DIANN_INDIVIDUALANALYSIS - Individual analysis
    withName: 'DIANN_INDIVIDUALANALYSIS' {
        ext.args = {
            [
                params.diann_debug ? "--verbose ${params.diann_debug}" : '',
                "--no-ifs-removal",
                "--no-main-report",
                "--relaxed-prot-inf",
                meta.pg_level ? "--pg-level ${meta.pg_level}" : '',
                meta.mass_acc_ms2 ? "--mass-acc ${meta.mass_acc_ms2}" : '',
                meta.mass_acc_ms1 ? "--mass-acc-ms1 ${meta.mass_acc_ms1}" : '',
                meta.scan_window ? "--window ${meta.scan_window}" : ''
            ].findAll { it != '' }.join(' ').trim()
        }
    }

    // DIANN_FINALQUANTIFICATION - Final quantification
    withName: 'DIANN_FINALQUANTIFICATION' {
        ext.args = [
            params.diann_debug ? "--verbose ${params.diann_debug}" : '',
            "--relaxed-prot-inf",
            params.pg_level != null ? "--pg-level ${params.pg_level}" : '',
            params.species_genes ? "--species-genes" : '',
            "--use-quant",
            "--matrices",
            "--out diann_report.tsv",
            params.protein_level_fdr_cutoff != null ? "--qvalue ${params.protein_level_fdr_cutoff}" : '',
            "--matrix-qvalue 1",  // Permissive global protein q-value filter for test data
            params.diann_report_decoys ? "--report-decoys" : '',
            params.diann_export_xic ? "--xic" : '',
            params.scan_window_automatic ? "--individual-windows" : (params.scan_window ? "--window ${params.scan_window}" : '')
        ].findAll { it != '' }.join(' ').trim()
    }

    withName: 'QUANTMSUTILS_DIANN2MZTAB' {
        ext.args = {
            [
                params.max_precursor_charge ? "--charge ${params.max_precursor_charge}" : '',
                params.allowed_missed_cleavages != null ? "--missed_cleavages ${params.allowed_missed_cleavages}" : '',
                params.protein_level_fdr_cutoff != null ? "--qvalue_threshold ${params.protein_level_fdr_cutoff}" : '',
                params.enable_diann_mztab ? "--enable_diann2mztab" : '',
                meta.dia_params ? "--dia_params \"${meta.dia_params}\"" : ''
            ].findAll { it != '' }.join(' ').trim()
        }
    }
}
